plugins {
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.136'
    id 'idea'
    id 'top.wmsnp.librarian' version '0.1.0'
    id 'top.wmsnp.brigadier' version '0.1.0'
}

tasks.named('wrapper', Wrapper).configure {
    distributionType = Wrapper.DistributionType.BIN
}

group = mod_group_id
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

base {
    archivesName = "${mod_id}-${minecraft_version_range}-${mod_version}"
}

dependencies {
    compileOnly "org.jspecify:jspecify:1.0.0"
}

librarian {
    mod("sodium", "clientRuntimeOnly")
    mod("iris", "compileOnly", "clientRuntimeOnly")
}

brigadier {
    sourceFiles.from("src/main/templates/commands.yaml")
}

neoForge {
    version = librarian.loaderVersion
    if (project.hasProperty('parchment_mappings_version') && project.hasProperty('parchment_minecraft_version') && project.parchment_mappings_version && project.parchment_minecraft_version) {
        parchment {
            mappingsVersion = project.parchment_mappings_version
            minecraftVersion = project.parchment_minecraft_version
        }
    }
    runs {
        client {
            client()
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        server {
            server()
            programArgument '--nogui'
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        gameTestServer {
            type = "gameTestServer"
            systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        }

        data {
            clientData()
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        configureEach {
            systemProperty 'forge.logging.markers', 'REGISTRIES'
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

tasks.named("processResources", ProcessResources).configure {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            neo_version            : librarian.loaderVersion,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
    ]
    inputs.properties replaceProperties
    filesMatching("META-INF/*.toml") {
        expand replaceProperties
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/repo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Main-Class': 'top.wmsnp.caps.ClickRun'
    }
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}